// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

	Mux16(a=instruction, b=ALUout, sel=instruction[15], out[0..14]=inA);
	And(a=instruction[15], b=instruction[3], out=writeM);
	ARegister(in[0..14]=inA, in[15]=false, load=putInA, out=outA, out[0..14]=addressM);
	DRegister(in=ALUout, load=putInD, out=outD);
	Mux16(a=outA, b=inM, sel=instruction[12], out=AorM);
	ALU(x=outD, y=AorM, zx=instruction[11], nx=instruction[10], zy=instruction[9] ,ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=ALUout,zr=zr, ng=ng);//instruction[6..11] codes the instruction Maybe a better inteface is one where you just put a bus in the ALU
	PC(in=outA, load=pcjump ,inc=true, reset=reset, out[0..14]=pc); 	//inc is always 1




	//CONTROLS
	//---------------------------------------------------

	//****************************************************
							
	
	//****************************************************
	//RegisterA control
	Not(in=instruction[15], out=Ainstruction);
	And(a=instruction[15], b=instruction[5], out=CinstPutinA);		//An A instruction gets saved instantly!
	Or(a=Ainstruction, b=CinstPutinA, out=putInA);
							
	//****************************************************
	//RegisterD control
	And(a=instruction[15], b=instruction[4], out=putInD);
							

	//****************************************************
	//PC control
	And(a=instruction[2], b=instruction[1], out=w1);
	And(a=w1, b=instruction[0], out=Jsactive);		//Unconditional jump(all active)
	
	Or(a=zr, b=ng, out=zrORng);
	Not(in=zrORng, out=strictlyPositiveOutM);
	And(a=instruction[0], b=strictlyPositiveOutM, out=strictPositive); //outM is strictly positive jump satisfied(you can jump to address in A!)
	And(a=ng, b=instruction[2], out=strictNegative);	//outM is strictly negative jump satisfied
	And(a=zr, b=instruction[1], out=strictZero);			//outM is zero jump satisfied
	Or(a=strictPositive, b=strictNegative, out=strict);
	Or(a=strict, b=strictZero, out=Jsatisfied);

	Or(a=Jsactive, b=Jsatisfied, out=pcjumpOnlyIf);		//One jump condition atleast is satisfied so we can tell the counter to jump
	And(a=pcjumpOnlyIf, b=instruction[15], out=pcjump);		//only jump if it is a C-Instruction
								//Use ALU's control Output: This is Wrong!! We must also verify that it is not a C-Instruction
								
}
